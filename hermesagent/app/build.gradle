apply plugin: 'com.android.application'


android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.virjar.hermes.hermesagent"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 6
        versionName "1.6"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags ""
            }
        }
    }

    signingConfigs {
        release {
            storeFile rootProject.file('deploy/hermes_key')
            storePassword "hermes"
            keyAlias "hermes"
            keyPassword "hermes"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        //debug ,也使用release的key，hermes代码无法在线调试，所以没有debug的意义
        debug {
            signingConfig signingConfigs.release
        }
    }


    sourceSets {
        main {
            manifest.srcFile 'src/main/AndroidManifest.xml'
            java.srcDirs = ['src/main/java', 'src/main/aidl']
            resources.srcDirs = ['src/main/java', 'src/main/aidl']
            aidl.srcDirs = ['src/main/aidl']
            res.srcDirs = ['src/main/res']
            assets.srcDirs = ['src/main/assets']
            jniLibs.srcDirs = ['libs']

        }
    }

//    externalNativeBuild {
//        cmake {
//            path "CMakeLists.txt"
//        }
//    }
    lintOptions {
        abortOnError false
    }

}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    testImplementation 'junit:junit:4.12'

    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'org.apache.commons:commons-lang3:3.7'
    // implementation project(':jadb')
    implementation project(':hermes-api')
    // implementation project(':AndroidAsync')
    implementation 'com.jaredrummler:android-processes:1.1.1'

    compileOnly 'de.robv.android.xposed:api:82'
    compileOnly 'org.projectlombok:lombok:1.18.2'
    annotationProcessor 'org.projectlombok:lombok:1.18.2'
    implementation 'com.alibaba:fastjson:1.2.7'
    implementation 'commons-io:commons-io:2.6'
    implementation 'com.google.guava:guava:23.4-android'
    implementation 'net.dongliu:apk-parser:2.6.2'
    //直接导入netty-all 会引发dex方法数量溢出
    //implementation 'io.netty:netty-all:4.1.29.Final'

    //for dbflow
    def dbflow_version = "4.2.4"
    annotationProcessor "com.github.Raizlabs.DBFlow:dbflow-processor:${dbflow_version}"
    implementation "com.github.Raizlabs.DBFlow:dbflow-core:${dbflow_version}"
    implementation "com.github.Raizlabs.DBFlow:dbflow:${dbflow_version}"

    //日志模块
    compile 'org.slf4j:slf4j-api:1.7.25'
    compile 'com.github.tony19:logback-android:1.3.0-2'
}


task insertXposedBridgeApk(type: Copy, dependsOn: ':xposedbridge:assembleRelease') {
    from file('../xposedbridge/build/outputs/apk/release/xposedbridge-release.apk')
    into file('./src/main/assets/')
}

tasks.withType(JavaCompile) {
    compileTask -> compileTask.dependsOn(insertXposedBridgeApk)
}

task cleanXposedBridgeApk(type: Delete) {
    delete file('./src/main/assets/xposedbridge-release.apk')
}
